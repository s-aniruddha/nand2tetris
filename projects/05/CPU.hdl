// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    Not(in = instruction[15], out = notinstruction); 
    And(a = instruction[15], b = instruction[11], out = zx);
    And(a = instruction[15], b = instruction[10], out = nx);
    And(a = instruction[15], b = instruction[9], out = zy);
    And(a = instruction[15], b = instruction[8], out = ny);
    And(a = instruction[15], b = instruction[7], out = f);
    And(a = instruction[15], b = instruction[6], out = no);
    ALU (x = ALUinput1, y = ALUinput2, zx = zx, nx = nx, zy = zy, ny = ny, f = f, no = no, out = ALUoutput, out = outM, zr = zr, ng = ng);
    Or(a = zr, b = ng, out = notpos);
    Not(in = zr, out = notzr);
    Not(in = ng, out = notng);
    And(a = notzr, b = notng, out = pos);
    

    And(a = instruction[15], b = instruction[4], out = loadD);
    DRegister(in = ALUoutput, load = loadD, out = ALUinput1);   //Dregister


    Mux16(a = instruction,b = ALUoutput,sel = instruction[15], out = Ainput);  //A register input
    Or(a = notinstruction, b = instruction[5], out = loadA);
    ARegister(in = Ainput, load = loadA, out[0..14] = addressM, out = address); //Aregister


    Mux16(a = address, b = inM, sel = instruction[12], out = ALUinput2);

    And(a = instruction[0], b = pos, out = loadPC1);
    And(a = instruction[1], b = zr, out = loadPC2);
    And(a = instruction[2], b = ng, out = loadPC3);
    Or(a = loadPC1, b = loadPC2, out = loadPC4);
    Or(a = loadPC4, b = loadPC3, out = loadPC5);
    And(a = instruction[15], b = loadPC5, out = loadPC); 
    PC (in = address, load = loadPC, inc = true, reset = reset, out[0..14] = pc);


    And(a = instruction[15],b = instruction[3], out = writeM);
}